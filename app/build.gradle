apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId "ie.macinnes.tvheadend"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode System.getenv("APP_VERSION_CODE") as Integer ?: 0
        versionName project.ext.gitVersionName

        setProperty("archivesBaseName", applicationId)

        buildConfigField "boolean", "ACRA_ENABLED", "false"
        buildConfigField "String", "ACRA_REPORT_URI", "\"\""
    }

    signingConfigs {
        if (rootProject.file("keystore.properties").exists()) {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

            release {
                storeFile new File(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-SNAPSHOT"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (signingConfigs.hasProperty("release")) {
                signingConfig signingConfigs.release
            }

            if (rootProject.file("acra.properties").exists()) {
                buildConfigField "boolean", "ACRA_ENABLED", "true"
                def acraProperties = new Properties()
                acraProperties.load(new FileInputStream(rootProject.file("acra.properties")))
                buildConfigField "String", "ACRA_REPORT_URI", "\"" + acraProperties.report_uri + "\""
            }
        }
    }

    productFlavors {
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'ie.macinnes.htsp:android-htsp:v0.0.1a19'
    // Used for testing local HTSP lib builds
//    compile(name: 'library-debug', ext: 'aar')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Used for testing local libvlc builds
//    compile(name: 'vlc-android-sdk-release', ext: 'aar')
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:leanback-v17:25.2.0'
    compile 'com.android.support:preference-leanback-v17:25.2.0'
    compile 'com.google.android.exoplayer:exoplayer:r2.2.0k1'
    compile 'com.google.android.exoplayer:extension-ffmpeg:r2.2.0k1'
    // Used for testing local exoplayer builds
//    compile(name: 'library-release', ext: 'aar')
    // Used for testing local exoplayer-ffmpeg builds
//    compile(name: 'extension-ffmpeg-release', ext: 'aar')
    compile 'ch.acra:acra:4.9.2'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
}
